buildscript {
    ext {
        kotlinVersion = '1.2.21'
        springBootVersion = '1.5.10.RELEASE'
        springIOPlatformVersion = 'Brussels-SR7'
        springCloudVersion = 'Edgware.SR2'
        pivotalCloudServicesVersion = '1.6.1.RELEASE'
        springFoxVersion = '2.8.0'
    }

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}"
    }
}

defaultTasks "clean", "build"

subprojects {
    // update for CVE-2017-5929
    ext['logback.version'] = '1.2.3'
    // update for HTTPCLIENT-1803
    ext['ribbon.version'] = '2.2.4'

    repositories {
        jcenter()
        mavenCentral()
    }

    // don't apply plugins to 'container' folders to avoid /build outputs
    if (['applications', 'components'].contains(name)) {
        return
    }

    apply plugin: 'kotlin'
    apply plugin: 'kotlin-spring'
    apply plugin: 'io.spring.dependency-management'

    group = 'io.undertree.skel.boot'
    version = '0.0.1-SNAPSHOT'

    sourceCompatibility = 1.8

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    dependencyManagement {
        imports {
            mavenBom "io.spring.platform:platform-bom:${springIOPlatformVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "io.pivotal.spring.cloud:spring-cloud-services-dependencies:${pivotalCloudServicesVersion}"
        }
        dependencies {
            // todo test this with snyk
        }
    }
}
