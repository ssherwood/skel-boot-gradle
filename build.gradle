buildscript {
    ext {
        kotlinVersion = '1.1.60'
        springBootVersion = '1.5.8.RELEASE'
        springIOPlatformVersion = 'Brussels-SR5'
        springCloudVersion = 'Camden.SR7'
        pivotalCloudServicesVersion = '1.4.1.RELEASE'
    }

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}"
    }
}

defaultTasks "clean", "build"

subprojects {

    repositories {
        mavenCentral()
        jcenter()
    }

    // don't apply plugins to 'container' folders to avoid /build outputs
    if (['applications', 'components'].contains(name)) {
        return
    }

    apply plugin: 'kotlin'
    apply plugin: 'kotlin-spring'
    apply plugin: 'io.spring.dependency-management'

    group = 'io.undertree.skel.boot'
    version = '0.0.1-SNAPSHOT'

    sourceCompatibility = 1.8

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    dependencyManagement {
        imports {
            mavenBom "io.spring.platform:platform-bom:${springIOPlatformVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "io.pivotal.spring.cloud:spring-cloud-services-dependencies:${pivotalCloudServicesVersion}"
        }
    }
}
